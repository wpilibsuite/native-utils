plugins {
    id 'com.gradle.plugin-publish'
    id 'java-gradle-plugin'
    id 'idea'
    id 'maven-publish'
    id 'groovy'
    id 'java'
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

dependencies {
    // For some utility classes. We don't actually apply DeployUtils to the FRCToolchain,
    // but we do in GradleRIO
    api 'org.wpilib:DeployUtils:2027.0.0'
    api 'de.undercouch:gradle-download-task:5.6.0'

    testImplementation('org.spockframework:spock-core:2.0-M4-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation gradleTestKit()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

base {
    archivesName = "Toolchain"
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

if (project.hasProperty('RunToolchainTests')) {
    test {
        environment "SPOCK_RUN_TOOLCHAINS", "true"
    }
}

gradlePlugin {
    website = 'https://github.com/wpilibsuite/native-utils'
    vcsUrl = 'https://github.com/wpilibsuite/native-utils'
    plugins {
        toolchain {
            id = 'org.wpilib.Toolchain'
            displayName = 'Toolchain'
            implementationClass = 'org.wpilib.toolchain.ToolchainPlugin'
            description = 'FRC Toolchain Standalone Gradle Plugin'
            tags = ['frc', 'wpilib', 'gradlerio']
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:all,-missing', true)
}
