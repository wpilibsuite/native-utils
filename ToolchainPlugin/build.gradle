plugins {
    id 'com.gradle.plugin-publish'
    id 'java-gradle-plugin'
    id 'idea'
    id 'maven-publish'
    id 'groovy'
    id 'java'
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

dependencies {
    // For some utility classes. We don't actually apply EmbeddedTools to the FRCToolchain,
    // but we do in GradleRIO
    api 'edu.wpi.first:EmbeddedTools:2021.02.06'
    api 'de.undercouch:gradle-download-task:4.0.1'

    testImplementation('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation gradleTestKit()
}

archivesBaseName = "Toolchain"

sourceCompatibility = 11
targetCompatibility = 11

pluginBundle {
    website = 'https://github.com/wpilibsuite/GradleRIO'
    vcsUrl = 'https://github.com/wpilibsuite/GradleRIO'
    description = 'FRC Toolchain Standalone Gradle Plugin'
    tags = ['frc', 'wpilib', 'gradlerio']
}

if (project.hasProperty('RunToolchainTests')) {
    test {
        environment "SPOCK_RUN_TOOLCHAINS", "true"
    }
}

gradlePlugin {
    plugins {
        toolchain {
            id = 'edu.wpi.first.Toolchain'
            displayName = 'Toolchain'
            implementationClass = 'edu.wpi.first.toolchain.ToolchainPlugin'
        }
    }
}
