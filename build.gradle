plugins {
    id "com.gradle.plugin-publish" version "0.9.9"
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin" version "2.2"
    id 'groovy'
    id 'java'
    id 'idea'
}

// Ensure that the WPILibVersioningPlugin is setup by setting the release type, if releaseType wasn't
// already specified on the command line
if (!hasProperty('releaseType')) {
    WPILibVersion {
        releaseType = 'dev'
    }
}

ext.pubVersion = ''
if (project.hasProperty("publishVersion")) {
    pubVersion = project.publishVersion
} else {
    pubVersion = WPILibVersion.version
}

if (pubVersion == '') {
    pubVersion = '0.0.1-unknown'
}

if (project.hasProperty('localPublish')) {
    pubVersion = '424242.0.0-local'
}

group = 'edu.wpi.first'
version pubVersion

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifact(jar) {
                if (project.hasProperty('localPublish')) {
                    classifier null
                    groupId 'gradle.plugin.' + group
                }
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/wpilibsuite/native-utils'
    vcsUrl = 'https://github.com/wpilibsuite/native-utils'
    description = 'This plugin provides native build utilities for FRC projects.'
    tags = ['groovy', 'native', 'utils', 'maven', 'frc', 'wpilib']

    plugins {
        NativeUtils {
            id = 'edu.wpi.first.NativeUtils'
            displayName = 'WPILib NativeUtils'
        }
    }
}

gradlePlugin {
    plugins {
        NativeUtils {
            id = 'edu.wpi.first.NativeUtils'
            implementationClass = 'edu.wpi.first.nativeutils.NativeUtils'
        }
    }
}

wrapper {
    gradleVersion = '4.9'
}
