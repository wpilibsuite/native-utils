plugins {
    id "com.gradle.plugin-publish" version "0.15.0"
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
    id 'java'
    id 'idea'
}

sourceCompatibility = 11
targetCompatibility = 11

allprojects {
    group = "edu.wpi.first"
    version = "2024.3.1"

    if (project.hasProperty('publishVersion')) {
        version = project.publishVersion
    }
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

dependencies {
    api project(':ToolchainPlugin')
    implementation 'org.ajoberstar.grgit:grgit-core:5.0.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.ow2.asm:asm:9.5'
    api 'edu.wpi.first:gradle-cpp-vscode:1.3.0'

    testImplementation('org.spockframework:spock-core:2.0-M4-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation gradleTestKit()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

pluginBundle {
    website = 'https://github.com/wpilibsuite/native-utils'
    vcsUrl = 'https://github.com/wpilibsuite/native-utils'
    description = 'This plugin provides native build utilities for FRC projects.'
    tags = ['groovy', 'native', 'utils', 'maven', 'frc', 'wpilib']
}

gradlePlugin {
    plugins {
        NativeUtils {
            id = 'edu.wpi.first.NativeUtils'
            displayName = 'NativeUtils'
            implementationClass = 'edu.wpi.first.nativeutils.NativeUtils'
        }
    }
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

wrapper {
    gradleVersion = '8.1'
}

def examplesFolder = file("$rootDir/testing")

tasks.register('PatchExamples') {
    doLast {
        String regex = "(id\\s*?[\\\"|\\']edu\\.wpi\\.first\\.NativeUtils[\\\"|\\'].*?version\\s*?[\\\"|\\'])(.+?)([\\\"|\\'])";

        examplesFolder.eachFile { File file ->
            if (file.isDirectory() && file.name != '_archived') {
                def buildGradleFile = new File(file, 'build.gradle')
                if (buildGradleFile.exists() && buildGradleFile.isFile()) {
                    def text = buildGradleFile.text
                    text = text.replaceAll(regex, "id \"edu.wpi.first.NativeUtils\" version \"${version}\"")
                    buildGradleFile.text = text
                }
            }
        }
    }
}

build.dependsOn PatchExamples
jar.finalizedBy PatchExamples
